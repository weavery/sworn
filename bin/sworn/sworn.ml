(* This is free and unencumbered software released into the public domain. *)

open Cmdliner

let version = "0.3.0"  (* TODO: preprocess from VERSION *)
let js_version = "0.1"

exception Error of int * string * string

let sworn verbose paths output target _optimize =
  let fprintf = Format.fprintf in

  let eprintf = if Unix.isatty (Unix.descr_of_out_channel stderr)
    then Ocolor_format.eprintf
    else Format.eprintf
  in

  let output_channel =
    match output with None -> stdout | Some output_path -> open_out output_path
  in

  let output_formatter = Format.formatter_of_out_channel output_channel in

  let printf (format : ('a, Format.formatter, unit) format) : 'a =
    if Unix.isatty (Unix.descr_of_out_channel output_channel)
    then
      let formatter' = Ocolor_format.make_formatter output_formatter in
      Format.fprintf (Ocolor_format.unwrap_formatter formatter') format
    else Format.fprintf output_formatter format
  in

  let read_file path =
    let channel = open_in path in
    let contents = really_input_string channel (in_channel_length channel) in
    close_in channel;
    contents
  in

  let guess_target = function
    | None -> None
    | Some output -> begin match Filename.extension output with
        | ".js" -> Some Target.JS
        | ".swir" -> Some Target.SWIR
        | ".wasm" -> Some Target.Wasm
        | ".wat" -> Some Target.Wat
        | _ -> None
      end
  in

  let rec process_program program target =
    match target with
    | Target.Auto -> begin match guess_target output with
        | Some target -> process_program program target
        | None -> printf "@[<v>%a@]@?" Clarity.print_program program
      end
    | SWIR ->
      let program' = SWIR.compile_program program in
      fprintf output_formatter "@[<v>%a@]@?" SWIR.print_program program'
    | JS ->
      let program' = SWIR.compile_program program in
      fprintf output_formatter "// Generated by Sworn on %a@\n@\n"
        ISO8601.Permissive.pp_datetimezone (Unix.time (), 0.);
      fprintf output_formatter "clarity.requireVersion(\"%s\");@\n@\n" js_version;
      fprintf output_formatter "@[<v>%a@]@?" JavaScript.print_program program'
    | Wasm ->
      let program' = SWIR.compile_program program in
      let wasm_module = WebAssembly.compile_program program' in
      let wasm_binary = Wasm.Encode.encode wasm_module in
      output_string output_channel wasm_binary
    | Wat ->
      let program' = SWIR.compile_program program in
      let wasm_module = WebAssembly.compile_program program' in
      Wasm.Print.module_ output_channel 80 wasm_module
  in

  let process_file path =
    if verbose then eprintf "@{<yellow>Compiling %s...@}@." path;
    let input = read_file path in
    let program = Clarity.parse_program input in
    process_program program target
  in

  try `Ok (List.iter process_file paths)
  with Error (code, path, error) -> begin
    eprintf "@{<red>error:@} %s: %s@." path error;
    exit code
  end

let command =
  let doc = "compile Clarity contracts for SmartWeave" in
  let man = [
    `S Manpage.s_description;
    `P "$(tname) compiles Clarity contracts into SmartWeave contracts.";
    `S Manpage.s_bugs; `P "Report any bugs at <https://github.com/weavery/sworn/issues>." ]
  in
  Term.(ret (const sworn $ Options.verbose $ Options.files $ Options.output $ Options.target $ Options.optimize)),
  Term.info "sworn" ~version ~doc ~exits:Term.default_exits ~man

let () = Term.(exit @@ eval command)
